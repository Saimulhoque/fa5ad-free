#!/usr/bin/awk -f

#
# Copyright (C) 2018 by Dariusz Iwanoczko
# Project homepage: https://github.com/diwanoczko/fa5ad-free
#

BEGIN {
    COPYRIGHT_PROJECT = \
        "    Android drawable generated by fa5ad-free project:\n" \
        "    https://github.com/diwanoczko/fa5ad-free\n\n" \
        "    Resource generated base on Font Awesome 5 Free icons set:\n" \
        "    https://fontawesome.com/\n";

    COPYRIGHT_BRAND_ANNOTATION = \
        "\n    All brand icons are trademarks of their respective owners.\n" \
        "    Please do not use brand logos for any purpose except to represent the\n" \
        "    company, product, or service to which they refer.\n";
    
    file_string_xml = "values/fa-strings.xml"
    
    # Prepare xml string resource
    print "<?xml version=\"1.0\" encoding=\"utf-8\"?>" > file_string_xml;
    print "<!--" > file_string_xml;
    print COPYRIGHT_PROJECT > file_string_xml;
    print "-->" > file_string_xml;
    print "<resources>" > file_string_xml;
}

{
    fa_icon_prefix = "fa";
    
    if (FILENAME ~ /fa-brands\.js/) {
        fa_icon_prefix = "fab";
    } else if (FILENAME ~ /fa-regular\.js/) {
        fa_icon_prefix = "far";
    } else if (FILENAME~ /fa-solid\.js/) {
        fa_icon_prefix = "fas";
    }
    
    if ($0 ~ /^.*\".*\": \[[0-9]*, [0-9]*, \[\], \"f.*\", \".*\"\].*$/) {
        fa_icon_name = gensub(/^  \"(.*)\": \[([0-9]*), ([0-9]*), \[\], \"(f.*)\", \"([Mm].*)\"\].*$/, "\\1", "g", $0);                        #"
        fa_icon_viewport_width = gensub(/^  \"(.*)\": \[([0-9]*), ([0-9]*), \[\], \"(f.*)\", \"([Mm].*)\"\].*$/, "\\2", "g", $0);              #"
        fa_icon_viewport_height = gensub(/^  \"(.*)\": \[([0-9]*), ([0-9]*), \[\], \"(f.*)\", \"([Mm].*)\"\].*$/, "\\3", "g", $0);             #"
        fa_icon_value = gensub(/^  \"(.*)\": \[([0-9]*), ([0-9]*), \[\], \"(f.*)\", \"([Mm].*)\"\].*$/, "\\4", "g", $0);                       #"
        fa_icon_svg_path = gensub(/^  \"(.*)\": \[([0-9]*), ([0-9]*), \[\], \"(f.*)\", \"([Mm].*)\"\].*$/, "\\5", "g", $0);                    #"
        
        # Prepare String resource
        gsub("-", "_", fa_icon_name);
        fa_icon_name = fa_icon_prefix "_" fa_icon_name;
        fa_icon_value = "&#x" fa_icon_value ";";
        fa_string = "    <string name=\"" fa_icon_name "\">" fa_icon_value "</string>";
        
        # Append new generated String to string.xml resource file
        print fa_string > file_string_xml;
        
        # Calculate size of icon
        svg_width_dp = (fa_icon_viewport_width/512) * 24;
        svg_height_dp = (fa_icon_viewport_height/512) * 24;
        
        # Start to construct the resource
        xml = "<?xml version=\"1.0\" encoding=\"utf-8\"?>\n";
        
        # Copyright information
        xml = xml "<!--\n";
        xml = xml COPYRIGHT_PROJECT;

        if (fa_icon_prefix == "fab") {
            xml = xml COPYRIGHT_BRAND_ANNOTATION;
        }

        xml = xml "-->\n";

        # Vector android drawable resource
        xml = xml "<vector xmlns:android=\"http://schemas.android.com/apk/res/android\"\n";
        xml = xml "    android:width=\"" svg_width_dp "dp\"\n";
        xml = xml "    android:height=\"" svg_height_dp "dp\"\n";
        xml = xml "    android:viewportWidth=\"" fa_icon_viewport_width "\"\n";
        xml = xml "    android:viewportHeight=\"" fa_icon_viewport_height "\">\n";
        xml = xml "    <path\n";
        xml = xml "        android:fillColor=\"#FF000000\"\n";
        xml = xml "        android:pathData=\"" fa_icon_svg_path "\"\n";
        xml = xml "    />\n";
        xml = xml "</vector>";
        
        # Generate xml resource
        file_drawable_xml = "drawable/ic_" fa_icon_name ".xml";
        print xml > file_drawable_xml;
    }
}

END {
    print "</resources>" > file_string_xml;
}
